import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-info-plugin:9.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:16.0.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.62.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.27.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:3.0.1'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:4.29.1'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.palantir.configuration-resolver' version '0.2.0'
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

version gitVersion()

allprojects {
    version rootProject.version
    group 'com.palantir.docker.compose'

    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'
    apply from: "${rootDir}/gradle/publish-jar.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        annotationProcessor 'org.immutables:value'
        compileOnly 'org.immutables:value::annotations'
    }

    tasks.withType(JavaCompile).configureEach {
        options.errorprone {

            check("CatchBlockLogException", CheckSeverity.OFF)
            check("PreconditionsConstantMessage", CheckSeverity.OFF)
            check("PreferSafeLoggableExceptions", CheckSeverity.OFF)
            check("PreferSafeLoggingPreconditions", CheckSeverity.OFF)
            check("Slf4jConstantLogMessage", CheckSeverity.OFF)
            check("ValidateConstantMessage", CheckSeverity.OFF)
            check("PreconditionsConstantMessage", CheckSeverity.OFF)
            check('Slf4jLogsafeArgs', CheckSeverity.OFF)

        }
    }
}
