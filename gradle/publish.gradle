apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

tasks.build.dependsOn sourceJar, javadocJar

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
            artifact(javadocJar)
            version project.version.toString().replaceAll(/-.*/, '-SNAPSHOT')
        }
    }
}


artifactory {
    publish {
        contextUrl = 'https://oss.jfrog.org/artifactory'
        repository {
            repoKey = (project.version =~ /-/ ? 'oss-snapshot-local' : 'oss-release-local')
            username = System.env.BINTRAY_USER
            password = System.env.BINTRAY_KEY
            maven = true
        }
        defaults {
            publications (publishing.publications.bintray)
            properties = ['git': project.version.toString()]
        }
    }
}
artifactoryPublish.dependsOn 'generatePomFileForBintrayPublication', 'build'

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    pkg {
        userOrg = 'palantir'
        repo = 'releases'
        name = 'docker-compose-rule'
        licenses = ['Apache-2.0']
        publications = ['bintray']
    }
    publish = true;
}

bintrayUpload.onlyIf {
    println "Attempting to publish with project version: " + project.version.toString()

    def versionIsInCorrectFormat = project.version.toString() ==~ /\d+\.\d+\.\d+/

    if (!versionIsInCorrectFormat) {
      println "Cannot publish project with version " + project.version.toString() + 
              "; it must be in form 'major.minor.patch'"
    }

    System.env.BINTRAY_USER && System.env.BINTRAY_KEY && versionIsInCorrectFormat
}

bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'


